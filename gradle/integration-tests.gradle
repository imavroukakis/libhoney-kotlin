// Add integration test source sets
sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}


dependencies {
    integrationTestImplementation 'org.awaitility:awaitility:3.1.0'
    integrationTestImplementation 'com.google.guava:guava:25.1-jre'
    integrationTestImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'
}

// Define integration test task
task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter test
}

integrationTest {
    jacoco {
        destinationFile = file("$buildDir/jacoco-it/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco-it/classpathdumps")
    }
}

// Make sure 'check' task calls integration test
check.dependsOn integrationTest

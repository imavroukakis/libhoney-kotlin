buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.0.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'net.researchgate.release' version '2.6.0'
}

group 'io.honeycomb'

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.beust:klaxon:$klaxon_version"
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation 'ch.qos.logback:logback-classic:1.1.2'
    implementation 'io.github.microutils:kotlin-logging:1.5.4'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


test {
    useJUnitPlatform()
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'
}

apply from: 'gradle/integration-tests.gradle'

detekt {
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    config = files("$projectDir/config/detekt/detekt.yml")
    reports {
        xml {
            enabled = true
        }
        html {
            enabled = false
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/imavroukakis/libhoney-kotlin.git'
        version {
            name = project.version
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.bintray.pkg.name
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = false
        csv.enabled = false
    }
}

task jacocoMerge(type: JacocoMerge) {
    dependsOn jacocoTestReport
    executionData([test, integrationTest])
}

task coverageReport(type: JacocoReport) {
    getSourceDirectories().from(sourceSets.main.kotlin.srcDirs)
    getSourceDirectories().from(sourceSets.main.output.classesDirs)
    executionData jacocoMerge.destinationFile
    dependsOn jacocoTestReport, jacocoMerge
    reports {
        html.enabled = false
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.enabled = false
    }
}

afterReleaseBuild.dependsOn bintrayUpload
